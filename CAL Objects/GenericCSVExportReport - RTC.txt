OBJECT Report 50207 DAMExport
{
  OBJECT-PROPERTIES
  {
    Date=30.10.21;
    Time=06:24:47;
    Modified=Yes;
    Version List=DAM;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               Action1 := 'Tabellenschema exportieren';
               Action2 := 'Tabellendaten exportieren';
             END;

    }
    CONTROLS
    {
      { 1000000006;;Container;
                  ContainerType=ContentArea }

      { 1000000005;1;Group  ;
                  GroupType=Group }

      { 1000000008;2;Group  ;
                  GroupType=GridLayout;
                  Layout=Rows }

      { 1000000010;3;Group  ;
                  CaptionML=[DEU=Ordner fuer Exportdateien;
                             ENU=Folder for export files];
                  GroupType=Group }

      { 1000000003;4;Field  ;
                  Name=FolderForExportFiles;
                  SourceExpr=ExportToFolderPath;
                  ShowCaption=No }

      { 1000000002;3;Group  ;
                  CaptionML=[DEU=Tabellen-ID Filter fuer Export;
                             ENU=Table ID Filter for Export];
                  GroupType=Group }

      { 1000000004;4;Field  ;
                  Name=Table ID Filter for Export;
                  SourceExpr=ExportTableIDFilter;
                  ShowCaption=No }

      { 1000000007;3;Group  ;
                  GroupType=Group }

      { 1000000001;4;Field  ;
                  Name=ExportFieldsCtrl;
                  SourceExpr=Action1;
                  Editable=false;
                  OnDrillDown=BEGIN
                                StartSchemaDefinitionExport
                              END;

                  ShowCaption=No }

      { 1000000009;3;Group  ;
                  GroupType=Group }

      { 1000000000;4;Field  ;
                  Name=ExportTableIDFilterCtrl;
                  SourceExpr=Action2;
                  Editable=false;
                  OnDrillDown=BEGIN
                                StartTableExport
                              END;

                  ShowCaption=No }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ExportToFolderPath@1000000000 : Text;
      ExportTableIDFilter@1000000001 : Text;
      FieldSeparator@1000000003 : Text[1];
      DotNetFile@1000000004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File" RUNONCLIENT;
      DotNetEncoding@1000000005 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding" RUNONCLIENT;
      DotNetStreamWriter@1000000006 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter" RUNONCLIENT;
      Action1@1000000002 : Text;
      Action2@1000000007 : Text;

    PROCEDURE LoadSettings@1000000008(VAR _ExportToFolderPath@1000000009 : Text[1024];VAR _ExportTableIDFilter@1000000008 : Text[1024]);
    VAR
      _RecordLink@1000000002 : Record 2000000068;
      _AllObjWithCaption@1000000001 : Record 2000000058;
      _RecRef@1000000000 : RecordRef;
      _LineArray@1000000003 : ARRAY [10] OF Text[1024];
      InS@1000000004 : InStream;
      LineCount@1000000005 : Integer;
      LineText@1000000006 : Text[1024];
      i@1000000007 : Integer;
    BEGIN
      // Find Record Link
      _AllObjWithCaption.GET(_AllObjWithCaption."Object Type"::Codeunit,1);
      _RecRef.GETTABLE(_AllObjWithCaption);
      _RecordLink.SETRANGE("Record ID",_RecRef.RECORDID);
      _RecordLink.SETRANGE(Description,'DAM');
      IF NOT _RecordLink.FINDFIRST THEN EXIT;

      // Read Blob to Array
      _RecordLink.CALCFIELDS(Note);
      IF NOT _RecordLink.Note.HASVALUE THEN
      EXIT;
      CLEAR(_LineArray);
      _RecordLink.Note.CREATEINSTREAM(InS);
      WHILE NOT InS.EOS DO BEGIN
        LineCount += 1;
        InS.READTEXT(LineText);
        _LineArray[LineCount]:= LineText;
      END;

      FOR i:= 1 TO ARRAYLEN(_LineArray) DO BEGIN
          CASE i OF
            1: _ExportToFolderPath  := _LineArray[i];
            2: _ExportTableIDFilter := _LineArray[i];
          END;
      END;
    END;

    PROCEDURE SaveSettings@1000000009(_ExportToFolderPath@1000000007 : Text[1024];_ExportTableIDFilter@1000000006 : Text[1024]);
    VAR
      _RecordLink@1000000004 : Record 2000000068;
      _AllObjWithCaption@1000000000 : Record 2000000058;
      _RecRef@1000000001 : RecordRef;
      _Content@1000000005 : BigText;
      CRLF@1000000002 : Text[2];
      OutStream@1000000003 : OutStream;
    BEGIN
      // Create Record Link
      _AllObjWithCaption.GET(_AllObjWithCaption."Object Type"::Codeunit,1);
      _RecRef.GETTABLE(_AllObjWithCaption);
      _RecordLink.SETRANGE("Record ID",_RecRef.RECORDID);
      _RecordLink.SETRANGE(Description,'DAM');
      IF NOT _RecordLink.FINDFIRST THEN BEGIN
        _RecordLink.GET(_AllObjWithCaption.ADDLINK('','DAM'));
      END;
      CRLF[1]:=13;CRLF[2]:=10;
      _Content.ADDTEXT(_ExportToFolderPath + CRLF + _ExportTableIDFilter);

      // WriteBlobText
      CLEAR(_RecordLink.Note);
      IF FORMAT(_Content) = '' THEN BEGIN
        _RecordLink.MODIFY;
      END ELSE BEGIN
        _RecordLink.Note.CREATEOUTSTREAM(OutStream);
        OutStream.WRITETEXT(FORMAT(_Content));
        _RecordLink.MODIFY;
      END;
    END;

    PROCEDURE ExportTable@1109900001(ExportToFilePath@1000000001 : Text[1024];ExportTableID@1000000004 : Integer;ExportWithTitleLine@1000000007 : Boolean;VAR ExportDuration@1000000003 : Duration;IsFieldTableExport@1000000010 : Boolean;TableView@1000000011 : Text[1024]);
    VAR
      _Start@1000000000 : DateTime;
      _LastFieldNo@1000000002 : Integer;
      _ExportFieldBuffer@1000000005 : TEMPORARY Record 2000000041;
      _ExportRecRef@1000000006 : RecordRef;
      _NewFilePath@1000000008 : Text[1024];
      _FieldSeparator@1000000009 : Text[1];
      _Progress@1000000012 : Dialog;
      _TotalLines@1000000013 : Integer;
      _Step@1000000014 : Integer;
      _LastUpdate@1000000015 : DateTime;
      _OutFile@1000000016 : File;
      _outStream@1000000017 : OutStream;
      _fileMgt@1000000018 : Codeunit 419;
    BEGIN
      CLEAR(ExportDuration);

      IF ExportTableID = 0 THEN
       ERROR('ExportTableID not defined');
      IF ExportToFilePath = '' THEN
        ERROR('ExportToFilename not defined');
      _FieldSeparator[1] := 9; // TAB
      _Start := CURRENTDATETIME;

      //_OutFile.CREATETEMPFILE;
      //_OutFile.CREATEOUTSTREAM(_outStream);

      //DotNetStreamWriter := DotNetStreamWriter.StreamWriter(_outStream,DotNetEncoding.Unicode);
      //DotNetStreamWriter := DotNetFile.Create(ExportToFilePath);
      //DotNetStreamWriter.Close;
      DotNetStreamWriter := DotNetStreamWriter.StreamWriter(ExportToFilePath,FALSE,DotNetEncoding.Unicode);

      InitExport;
      LoadFieldList(_ExportFieldBuffer,ExportTableID,IsFieldTableExport);
      _ExportFieldBuffer.FINDLAST;
      _LastFieldNo := _ExportFieldBuffer."No.";
      _ExportRecRef.OPEN(ExportTableID,FALSE);
      _TotalLines := _ExportRecRef.COUNT;
      _Progress.OPEN(STRSUBSTNO('Export %1\@@@@@@@@@@@@@@@@@@@@@@1@\Tabelle %2 - %3 Zeilen',
                                 CONVERTSTR(ExportToFilePath,'\','/'),
                                 _ExportRecRef.CAPTION,
                                 _TotalLines));

      IF TableView <> '' THEN
        _ExportRecRef.SETVIEW(TableView);
      IF NOT _ExportRecRef.FINDSET(FALSE,FALSE) THEN EXIT;
      IF ExportWithTitleLine THEN
        CreateHeaderLine(_ExportFieldBuffer,_LastFieldNo); // Header
      // Lines
      _LastUpdate := CURRENTDATETIME + 500;
      REPEAT
        ExportTableRec(_ExportRecRef,_ExportFieldBuffer,_LastFieldNo,_FieldSeparator);

        _Step += 1;
        IF ABS(CURRENTDATETIME-_LastUpdate) > 500 THEN BEGIN
          _Progress.UPDATE(1,(10000*(_Step/_TotalLines)) DIV 1);
          _LastUpdate := CURRENTDATETIME;
        END;
      UNTIL _ExportRecRef.NEXT = 0;

      //ADOStream.SaveToFile(ExportToFilePath,2); // 2- Overwrite, 1- Default
      //ADOStream.Close;
      //CLEAR(ADOStream);
      //DotNetFile.WriteAllText(ExportToFilePath, Content,DotNetEncoding.Unicode);

      ExportDuration := CURRENTDATETIME - _Start;

      _Progress.CLOSE;

      DotNetStreamWriter.Close();
      //_OutFile.CLOSE();
    END;

    PROCEDURE ExportTableRec@1000000014(VAR _RecRef@1000000000 : RecordRef;VAR _ExportFieldBuffer@1000000002 : TEMPORARY Record 2000000041;_LastFieldNo@1000000001 : Integer;_FieldSeparator@1000000004 : Text[1]);
    VAR
      _ValueAsText@1000000003 : Text[1024];
      _ValueAsTextBT@1000000011 : BigText;
      _CurrTableRef@1000000005 : RecordRef;
      _CurrFldRef@1000000009 : FieldRef;
      _Field@1000000010 : Record 2000000041;
      _TableNo@1000000006 : Integer;
      _FieldNo@1000000008 : Integer;
      _LineText@1000000007 : Text[1024];
      _Pos@1000000012 : Integer;
    BEGIN
      _ExportFieldBuffer.FINDSET;
      REPEAT
        CLEAR(_ValueAsText);CLEAR(_ValueAsTextBT);
        // Format Field in XML Format, Zero numbers are empty (Save Space)
        CASE TRUE OF
          (_ExportFieldBuffer.TableNo = DATABASE::Field) AND (_ExportFieldBuffer."No."=50000): BEGIN
            CLEAR(_CurrTableRef);
            _TableNo := _RecRef.FIELD(1).VALUE;
            _CurrTableRef.OPEN(_TableNo,FALSE);
            _ValueAsText := _CurrTableRef.CAPTION;
          END;
          (_ExportFieldBuffer.TableNo = DATABASE::Field) AND (_ExportFieldBuffer."No."=50001): BEGIN
            _TableNo := _RecRef.FIELD(1).VALUE;
            CLEAR(_CurrTableRef);
            _CurrTableRef.OPEN(_TableNo,FALSE);
            _ValueAsText := GetListOfKeyFieldIDs(_CurrTableRef.NUMBER);
          END;
          //OptionString
          (_ExportFieldBuffer.TableNo = DATABASE::Field) AND (_ExportFieldBuffer."No."=50002): BEGIN
            _RecRef.SETTABLE(_Field);
            IF _Field.Type = _Field.Type::Option THEN BEGIN
              CLEAR(_CurrTableRef);
              _CurrTableRef.OPEN(_Field.TableNo,FALSE);
              _CurrFldRef := _CurrTableRef.FIELD(_Field."No.");
              GetListOfOptionString(_CurrFldRef,_ValueAsTextBT);
            END;
          END;
          (_ExportFieldBuffer.TableNo = DATABASE::Field) AND (_ExportFieldBuffer."No."=50003): BEGIN
            _RecRef.SETTABLE(_Field);
            IF _Field.Type = _Field.Type::Option THEN BEGIN
              CLEAR(_CurrTableRef);
              _CurrTableRef.OPEN(_Field.TableNo,FALSE);
              _CurrFldRef := _CurrTableRef.FIELD(_Field."No.");
              //mask leading "
              IF (STRLEN(_CurrFldRef.OPTIONCAPTION)>1) AND (COPYSTR(_CurrFldRef.OPTIONCAPTION,1,2)=' ,') THEN BEGIN
                _ValueAsText := '" "'+COPYSTR(_CurrFldRef.OPTIONCAPTION,2);
              END ELSE BEGIN
                _ValueAsText := _CurrFldRef.OPTIONCAPTION;
              END;
            END;
          END;
          ELSE _ValueAsText := GetFormattedFieldValue(_RecRef,_ExportFieldBuffer."No.");
        END; // END_CASE

        IF _ValueAsTextBT.LENGTH = 0 THEN BEGIN
           // field content is Text
          _ValueAsText := ReplaceStr(_ValueAsText,FieldSeparator,'');  // Remove FieldSeparator from Value
          RemoveSpecialChars(_ValueAsText);
          AddText(_ValueAsText);
        END ELSE BEGIN
          // field content is Bigtext
          _Pos:=1;
          WHILE _Pos <= _ValueAsTextBT.LENGTH DO BEGIN
            _Pos += _ValueAsTextBT.GETSUBTEXT(_ValueAsText,_Pos,1024);
            AddText(_ValueAsText);
          END;
        END;

        // Add Closing Sign
        IF _LastFieldNo = _ExportFieldBuffer."No." THEN
          AddTextLn('')
        ELSE
          AddText(_FieldSeparator);

      UNTIL _ExportFieldBuffer.NEXT = 0;
    END;

    PROCEDURE CreateHeaderLine@1000000013(VAR _ExportFieldBuffer@1000000001 : TEMPORARY Record 2000000041;_LastFieldNo@1000000000 : Integer);
    VAR
      _ValueAsText@1000000002 : Text[1024];
    BEGIN
      _ExportFieldBuffer.FINDSET;
      REPEAT
        _ValueAsText := ReplaceStr(_ExportFieldBuffer."Field Caption",FieldSeparator,'');  // Remove FieldSeparator from Value
        AddText(_ValueAsText + FieldSeparator)
      UNTIL _ExportFieldBuffer.NEXT = 0;
      AddTextLn(''); // Add LineBreak
    END;

    PROCEDURE AddText@1000000003(Text@1000000000 : Text[1024]);
    BEGIN
      //ADOStream.WriteText(Text,0{LineBreak});
      //Content += Text;
      DotNetStreamWriter.Write(Text);
    END;

    PROCEDURE AddTextLn@1000000004(Text@1000000000 : Text[1024]);
    VAR
      CRLF@1000000001 : Text[2];
    BEGIN
      //ADOStream.WriteText(Text,1{LineBreak});
      CRLF[1]:=13;CRLF[2]:=10;
      //Content += Text + CRLF;
      DotNetStreamWriter.WriteLine(Text);
    END;

    PROCEDURE GetListOfOptionString@1000000021(_FieldRef@1000000000 : FieldRef;VAR _Result@1000000006 : BigText);
    VAR
      _OptionCounter@1000000001 : Integer;
      _OptionString@1000000002 : Text[1024];
      i@1000000003 : Integer;
      _OptionElement@1000000004 : Text[1024];
      _SubStr@1000000007 : Text[30];
      Letters@1000000005 : TextConst 'DEU=abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ';
    BEGIN
      _OptionString := _FieldRef.OPTIONSTRING;
      _OptionCounter := 1;
      FOR i := 1 TO STRLEN(_OptionString) DO BEGIN
        IF _OptionString[i] = ',' THEN
          _OptionCounter += 1;
      END;

      FOR i := 1 TO _OptionCounter DO BEGIN
        _OptionElement := SELECTSTR(i,_OptionString);
        IF (DELCHR(_OptionElement,'=',Letters) = '') THEN BEGIN // ContainsOnlyLetters
          _Result.ADDTEXT(STRSUBSTNO('%1,',_OptionElement));
        END ELSE BEGIN
          _Result.ADDTEXT(STRSUBSTNO('"%1",',_OptionElement));
        END;
      END;
      // remove last
      IF _Result.LENGTH > 0 THEN
        _Result.GETSUBTEXT(_SubStr,_Result.LENGTH,1);
      IF _SubStr = ',' THEN
        _Result.GETSUBTEXT(_Result,1,_Result.LENGTH-1);
    END;

    PROCEDURE GetListOfKeyFieldIDs@1109900003(TableNo@1109900000 : Integer) KeyFieldsList : Text[1024];
    VAR
      Key@1109900001 : Record 2000000063;
      RecRef@1000000001 : RecordRef;
      KeyRef@1000000002 : KeyRef;
      FldRef@1000000003 : FieldRef;
      i@1000000004 : Integer;
    BEGIN
      RecRef.OPEN(TableNo);
      KeyRef := RecRef.KEYINDEX(1);
      FOR i := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FldRef := KeyRef.FIELDINDEX(i);
        KeyFieldsList += FORMAT(FldRef.NUMBER) + ',';
      END;
      KeyFieldsList := DELCHR(KeyFieldsList,'>',',');
    END;

    PROCEDURE GetFormattedFieldValue@1000000010(VAR _RecRef@1000000008 : RecordRef;_FieldNo@1000000009 : Integer) _Result : Text[1024];
    VAR
      _FieldRef@1000000000 : FieldRef;
      _Value@1000000007 : Variant;
      _Integer@1000000002 : Integer;
      _Text@1000000003 : Text[1024];
      _Decimal@1000000004 : Decimal;
      _Date@1000000005 : Date;
      _Time@1000000001 : Time;
      _Boolean@1000000006 : Boolean;
      _Field@1000000010 : Record 2000000041;
    BEGIN
      //* DATENTYP-MAPPING UM FORMAT-ANWEISUNG IM CODE ZU MINIMIEREN
      _FieldRef := _RecRef.FIELD(_FieldNo);
      _FieldRef.VALUE := _FieldRef.VALUE;
      CASE UPPERCASE(FORMAT(_FieldRef.TYPE)) OF
        'BOOLEAN': BEGIN; _Boolean := _FieldRef.VALUE; _Result  := '0';   IF _Boolean THEN      _Result := '1'; END;
        'INTEGER': BEGIN; _Integer := _FieldRef.VALUE; IF _Integer <> 0 THEN _Result := FORMAT(_Integer,0,9); END;
        'OPTION':  BEGIN; _Integer := _FieldRef.VALUE; _Result := FORMAT(_Integer,0,9); END;
        'DECIMAL': BEGIN; _Decimal := _FieldRef.VALUE; IF _Decimal <> 0 THEN _Result := FORMAT(_Decimal,0,9); END;
        'DATE':    BEGIN; _Date    := _FieldRef.VALUE; IF _Date <> 0D THEN   _Result := FORMAT(_Date,0,9);    END;
        'TIME':    BEGIN; _Time := _FieldRef.VALUE;    IF _Time <> 0T THEN   _Result := FORMAT(_Time,0,9);    END;
        'CHAR','TEXT','CODE':   _Result := _FieldRef.VALUE;
        ELSE _Result := FORMAT(_FieldRef.VALUE,0,9);
      END; // END_CASE

      IF (_RecRef.NUMBER = DATABASE::Field) AND (UPPERCASE(FORMAT(_FieldRef.TYPE)) = 'OPTION')THEN BEGIN
        _RecRef.SETTABLE(_Field);
        IF (_FieldRef.NUMBER=_Field.FIELDNO(Type)) THEN
          CASE _Field.Type OF
            _Field.Type::TableFilter: _Result := '0';
            _Field.Type::RecordID   : _Result := '1';
            _Field.Type::Text       : _Result := '2';
            _Field.Type::Date       : _Result := '3';
            _Field.Type::Time       : _Result := '4';
            _Field.Type::DateFormula: _Result := '5';
            _Field.Type::Decimal    : _Result := '6';
            _Field.Type::Binary     : _Result := '7';
            _Field.Type::BLOB       : _Result := '8';
            _Field.Type::Boolean    : _Result := '9';
            _Field.Type::Integer    : _Result := '10';
            _Field.Type::Code       : _Result := '11';
            _Field.Type::Option     : _Result := '12';
            _Field.Type::BigInteger : _Result := '13';
            _Field.Type::Duration   : _Result := '14';
            _Field.Type::GUID       : _Result := '15';
            _Field.Type::DateTime   : _Result := '16';
          END;
      END;
    END;

    PROCEDURE RemoveSpecialChars@1109900000(VAR Text@1109900000 : Text[1024]);
    VAR
      CharArray@1109900001 : Text[30];
      Chunk@1000000000 : Text[1];
      TextOLD@1000000001 : BigText;
      i@1000000002 : Integer;
    BEGIN
      CharArray[1] := 9; // TAB
      CharArray[2] := 10; // LF
      CharArray[3] := 13; // CR
      Text := DELCHR(Text,'=',CharArray);
    END;

    PROCEDURE ReplaceStr@1000000015(_Text@1000000000 : Text[1024];_FindWhat@1000000001 : Text[1024];_ReplaceWith@1000000002 : Text[1024]) _NewText : Text[1024];
    BEGIN
      WHILE STRPOS(_Text,_FindWhat) > 0 DO
        _Text := DELSTR(_Text,STRPOS(_Text,_FindWhat)) +
                  _ReplaceWith +
                  COPYSTR(_Text,STRPOS(_Text,_FindWhat)+STRLEN(_FindWhat));
      _NewText := _Text;
    END;

    PROCEDURE LoadFieldList@1000000018(VAR _ExportFields_FOUND@1000000001 : TEMPORARY Record 2000000041;_ExportTableID@1000000003 : Integer;IsFieldTableExport@1000000004 : Boolean);
    VAR
      _Fields@1000000000 : Record 2000000041;
      _ExportFieldBuffer@1000000002 : TEMPORARY Record 2000000041;
    BEGIN
      IF _ExportTableID <> 0 THEN
        _Fields.SETRANGE(TableNo,_ExportTableID);
      _Fields.SETFILTER(Type,'<>%1',_Fields.Type::BLOB);
      _Fields.SETFILTER(Class,'%1|%2',_Fields.Class::Normal,_Fields.Class::FlowField);
      _Fields.SETRANGE(Enabled,TRUE);
      _Fields.FINDSET;
      REPEAT
        _ExportFieldBuffer := _Fields;
        _ExportFieldBuffer.INSERT;
      UNTIL _Fields.NEXT = 0;

      IF IsFieldTableExport THEN BEGIN
        _ExportFieldBuffer.INIT;
        _ExportFieldBuffer.TableNo := DATABASE::Field;
        _ExportFieldBuffer."No." := 50000;
        _ExportFieldBuffer.FieldName := 'Table Caption';
        _ExportFieldBuffer."Field Caption" := 'Tabellenbezeichnung';
        _ExportFieldBuffer.Type := _ExportFieldBuffer.Type::Text;
        _ExportFieldBuffer.Len := 250;
        _ExportFieldBuffer.INSERT;

        _ExportFieldBuffer.INIT;
        _ExportFieldBuffer.TableNo := DATABASE::Field;
        _ExportFieldBuffer."No." := 50001;
        _ExportFieldBuffer.FieldName := 'Primary Key';
        _ExportFieldBuffer."Field Caption" := 'PrimÑrschlÅssel';
        _ExportFieldBuffer.Type := _ExportFieldBuffer.Type::Text;
        _ExportFieldBuffer.Len := 250;
        _ExportFieldBuffer.INSERT;

        _ExportFieldBuffer.INIT;
        _ExportFieldBuffer.TableNo := DATABASE::Field;
        _ExportFieldBuffer."No." := 50002;
        _ExportFieldBuffer.FieldName := 'OptionString';
        _ExportFieldBuffer.Type := _ExportFieldBuffer.Type::Text;
        _ExportFieldBuffer.Len := 250;
        _ExportFieldBuffer.INSERT;

        _ExportFieldBuffer.INIT;
        _ExportFieldBuffer.TableNo := DATABASE::Field;
        _ExportFieldBuffer."No." := 50003;
        _ExportFieldBuffer.FieldName := 'OptionCaption';
        _ExportFieldBuffer.Type := _ExportFieldBuffer.Type::Text;
        _ExportFieldBuffer.Len := 250;
        _ExportFieldBuffer.INSERT;
      END;

      _ExportFields_FOUND.COPY(_ExportFieldBuffer,TRUE);
    END;

    PROCEDURE IsTableInLicense@1000000007(ObjectID@1000000000 : Integer) : Boolean;
    VAR
      _LicensePermission@1000000001 : Record 2000000043;
    BEGIN
      IF NOT _LicensePermission.GET(_LicensePermission."Object Type"::Table,ObjectID) THEN
        EXIT(FALSE);
      EXIT(_LicensePermission."Execute Permission"<>_LicensePermission."Execute Permission"::" ");
    END;

    PROCEDURE InitExport@1000000017();
    BEGIN
      //CLEAR(ADOStream);
      //CREATE(ADOStream);
      // [LINEFieldSeparatorENUM LineFieldSeparator]
      // adCR    13  Indicates carriage return.
      // adCRLF  -1  Default. Indicates carriage return line feed.
      // adLF    10  Indicates line feed.
      //ADOStream.LineSeparator(-1);

      //ADOStream.Open;
      //ADOStream.Charset('iso-8859-1'); //UTF-8, default is unicode

      FieldSeparator[1] := 9; // TAB
    END;

    PROCEDURE LookUpObjIDFilter@1000000000();
    VAR
      AllObj@1000000000 : Record 2000000038;
    BEGIN
      AllObj.SETRANGE("Object Type",AllObj."Object Type"::Table);
      IF PAGE.RUNMODAL(0,AllObj) = ACTION::LookupOK THEN BEGIN
        IF ExportTableIDFilter = '' THEN
           ExportTableIDFilter += FORMAT(AllObj."Object ID")
         ELSE
           ExportTableIDFilter += '|' + FORMAT(AllObj."Object ID");
      END;
    END;

    PROCEDURE CheckFolderPathRTC@1000000001(FolderPathToCheck@1000000000 : Text[1024]);
    VAR
      FileMgt@1000000001 : Codeunit 419;
      ClientDirectoryHelper@1000000002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Directory" RUNONCLIENT;
    BEGIN
      IF NOT ClientDirectoryHelper.Exists(FolderPathToCheck) THEN
       ERROR(STRSUBSTNO('Ungueltiger Ordnerpfad: %1',FolderPathToCheck));
      IF ExportToFolderPath[STRLEN(ExportToFolderPath)]<>'\' THEN
        ExportToFolderPath += '\';
    END;

    PROCEDURE "----- Actions -----"@1000000006();
    BEGIN
    END;

    PROCEDURE StartTableExport@1000000005();
    VAR
      AllObjWithCaption@1000000000 : Record 2000000058;
      ExportToPath@1000000001 : Text[1024];
      ExportDuration@1000000002 : Duration;
      Progress@1000000003 : Dialog;
      ReadyMsg@1000000004 : Text[1024];
      TAB@1000000005 : Text[1];
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::TableData);
      AllObjWithCaption.SETFILTER("Object ID",ExportTableIDFilter);
      IF NOT AllObjWithCaption.FINDSET THEN EXIT;
      TAB[1] := 9; // TAB

      REPEAT
        IF NOT IsTableInLicense(AllObjWithCaption."Object ID") THEN
        MESSAGE('Achtung: Tabelle %1 - %2 ist nicht in der Lizenz.',AllObjWithCaption."Object ID",AllObjWithCaption."Object Caption");
      UNTIL AllObjWithCaption.NEXT = 0;

      ReadyMsg:='DAM EXPORT:';
      AllObjWithCaption.FINDSET;
      REPEAT
        ExportToPath := ExportToFolderPath + CONVERTSTR(AllObjWithCaption."Object Caption",'<>*\/|"','_______') +'.csv';
        ExportTable(ExportToPath,AllObjWithCaption."Object ID",TRUE,ExportDuration,FALSE,'');
        ReadyMsg += STRSUBSTNO('\%1:%2%3',AllObjWithCaption."Object Caption",TAB,ExportDuration);

      UNTIL AllObjWithCaption.NEXT = 0;
      MESSAGE(ReadyMsg)
    END;

    PROCEDURE StartSchemaDefinitionExport@1000000016();
    VAR
      AllObjWithCaption@1000000004 : Record 2000000058;
      ExportToPath@1000000003 : Text[1024];
      ExportDuration@1000000002 : Duration;
      Progress@1000000001 : Dialog;
      ReadyMsg@1000000000 : Text[1024];
      Field@1000000005 : Record 2000000041;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::TableData);
      AllObjWithCaption.SETRANGE("Object ID",DATABASE::Field);
      IF NOT AllObjWithCaption.FINDSET THEN EXIT;
      ReadyMsg:='DAM EXPORT:';
      REPEAT
        ExportToPath := ExportToFolderPath + 'Schema'+'.csv';
        Progress.OPEN(STRSUBSTNO('Export %1',CONVERTSTR(ExportToPath,'\','/')));
        Field.SETRANGE(TableNo,0,2000000000);
        ExportTable(ExportToPath,AllObjWithCaption."Object ID",TRUE,ExportDuration,TRUE,Field.GETVIEW);
        ReadyMsg += STRSUBSTNO('\%1: %2',AllObjWithCaption."Object Caption",ExportDuration);
        Progress.CLOSE;
      UNTIL AllObjWithCaption.NEXT = 0;
      MESSAGE(ReadyMsg)
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

